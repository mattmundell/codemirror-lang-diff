@top Source {
  (line? newline)* line? (newline | eof)
}

line {
  Command |
  Index |
  BinaryChange |
  FileChange |
  NewFile |
  OldFile |
  Location |
  Context |
  Addition |
  Deletion |
  Similarity |
  noNewLineAtTheEndOfNewFile
}

noise {
    anything
}

Similarity {
    similarityIndex digit digit? digit? "%"
}

Addition { leadingPlus anything? }

Deletion { leadingMinus anything? }

Location { locationStart Linerange " " Linerange " @@" anything? }

Context { leadingSpace anything? newline }

Linerange { ("-" | "+") digit+ ("," digit+)? }

NewFile { plusNew Filename }

OldFile { minusOld Filename }

Command {
    diffGit anything
}

Index {
    indexStart Commit ".." Commit  (" " Mode)?
}

Commit { 
    hex hex hex hex hex hex hex |
    hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex 
}

BinaryChange {
    binaryFiles anything
}

FileChange { 
    FileRenamedFrom |
    FileRenamedTo |
    FileNew |
    FileDeleted
}
FileRenamedFrom { renameFrom Filename }
FileRenamedTo { renameTo Filename }
FileDeleted { deletedFile Mode }
FileNew { newFile Mode }

Mode { digit digit digit digit digit digit }

Filename { anything }

@tokens {
    diffGit { "diff --git " }
    indexStart { "index " }
    leadingSpace { " " }
    plusNew { "+++ " }
    minusOld { "--- " }
    leadingPlus { "+" }
    leadingMinus { "-" }
    locationStart { "@@ " }
    similarityIndex { "similarity index " }
    binaryFiles { "Binary files " }
    renameFrom { "rename from " }
    renameTo { "rename to " }
    deletedFile { "deleted file mode " }
    newFile { "new file mode " }
    noNewLineAtTheEndOfNewFile { "\\ No newline at end of file" }

    hex { $[a-f0-9] }
    newline { "\r"? "\n" }
    eof { @eof }
    anything { ![\r\n]+ }
    digit { @digit }

    @precedence { diffGit, indexStart, leadingSpace, plusNew, minusOld, leadingPlus, leadingMinus, locationStart, similarityIndex, binaryFiles, noNewLineAtTheEndOfNewFile, renameFrom, renameTo, deletedFile, newFile, anything }
}
