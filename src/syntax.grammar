@top Source {
  (line? newline)* line?
}

line {
  Command |
  Index |
  BinaryChange |
  FileChange |
  NewFile |
  OldFile |
  Location |
  Context |
  Addition |
  Deletion |
  Similarity |
  noNewLineAtTheEndOfNewFile
}

Similarity {
    "similarity index " digit digit? digit? "%"
}

Addition { "+" anything? }

Deletion { "-" anything? }

Location { "@@ " Linerange " " Linerange " @@" anything? }

Context { " " anything? }

Linerange { ("-" | "+") digit+ ("," digit+)? }

NewFile { "+++ " Filename }

OldFile { "--- " Filename }

Command {
    "diff --git " anything
}

Index {
    "index " Commit ".." Commit  (" " Mode)?
}

Commit { 
    hex hex hex hex hex hex hex |
    hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex hex 
}

BinaryChange {
    "Binary files " anything
}

FileChange { 
    FileRenamedFrom |
    FileRenamedTo |
    FileNew |
    FileDeleted
}
FileRenamedFrom { "rename from " Filename }
FileRenamedTo { "rename to " Filename }
FileDeleted { "deleted file mode " Mode }
FileNew { "new file mode " Mode }

Mode { digit digit digit digit digit digit }

Filename { anything }

noNewLineAtTheEndOfNewFile { "\\ No newline at end of file" }

@tokens {
    hex { $[a-f0-9] }
    newline { "\r"? "\n" }
    anything { ![\r\n]+ }
    digit { @digit }
}
